dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(hocr, 0.7.0)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

VERSION_INFO=0:0:0
AC_SUBST(VERSION_INFO)

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
AM_PROG_LIBTOOL

AC_PATH_PROG(PKG_CONFIG, pkg-config, no)

PACKAGE_CFLAGS=-Wall
PACKAGE_LIBS=-lm

win32=no
case "${host_os}" in
  *mingw* | pw32* | cygwin*)
	win32="yes"
    AC_CHECK_TOOL(WINDRES, windres, :)
    PACKAGE_CFLAGS="$PACKAGE_CFLAGS -mms-bitfields -mno-cygwin"
esac
AM_CONDITIONAL(WITH_WIN32, test x"$win32" = "xyes")

AC_ARG_ENABLE(nls, [  --disable-nls		disable native language support], nls="$enableval", nls=yes)

if test "x$nls" = "xyes"; then
	GETTEXT_PACKAGE=hocr-gtk
	AC_SUBST(GETTEXT_PACKAGE)
	AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE", [Gettext package.])
	
	dnl Add the languages which your application supports here.
	ALL_LINGUAS="he"
	AM_GLIB_GNU_GETTEXT
	
	PO_DIR=po
	AC_SUBST(PO_DIR)
fi

dnl =======================================================================================

build_gtk=yes
gtk=no

AC_ARG_ENABLE(gtk, [  --disable-gtk		do not build hocr-gtk program], build_gtk="$enableval", build_gtk=yes)

if test "x$build_gtk" = "xyes"; then
	gtk_pkg_modules="gtk+-2.0 gthread-2.0"
	PKG_CHECK_MODULES(gtk, [$gtk_pkg_modules], gtk=yes, gtk=no)
	AC_SUBST(gtk_CFLAGS)
	AC_SUBST(gtk_LIBS)
fi

AM_CONDITIONAL(WITH_GTK, test "x$gtk" = "xyes")

dnl =======================================================================================

build_gtkspell=yes
gtkspell=no

AC_ARG_ENABLE(gtkspell, [  --disable-gtkspell	do not build hocr-gtk spellchecker], build_gtkspell="$enableval", build_gtkspell=yes)

if test "x$gtk" = "xyes"; then
	if test "x$build_gtkspell" = "xyes"; then
		gtkspell_pkg_modules="gtkspell-2.0"
		PKG_CHECK_MODULES(gtkspell, [$gtkspell_pkg_modules], gtkspell=yes, gtkspell=no)
		AC_SUBST(gtkspell_CFLAGS)
		AC_SUBST(gtkspell_LIBS)
	fi
fi

AM_CONDITIONAL(WITH_GTKSPELL, test "x$gtkspell" = "xyes")
AC_SUBST(WITH_GTKSPELL)

dnl =======================================================================================

build_cmd=yes
cmd=no

AC_ARG_ENABLE(cmd, [  --disable-cmd		do not build hocr program], build_cmd="$enableval", build_cmd=yes)

if test "x$build_cmd" = "xyes"; then
	cmd=yes
fi

AM_CONDITIONAL(WITH_CMD, test "x$cmd" = "xyes")

dnl =======================================================================================
dnl bindings 
dnl =======================================================================================

dnl =======================================================================================

AC_CHECK_PROG(have_swig, "swig", yes, no)

dnl =======================================================================================

build_python=no

AC_ARG_ENABLE(python, [  --enable-python	build python binding [default=no]], build_python="$enableval", build_python=no)

AC_ARG_WITH(python-sitelib-dir, [  --with-python-sitelib-dir=PATH	path to python site lib ])

AC_CHECK_PROG(have_python, "python", yes, no)

if test "x$have_python" = "xyes"; then
	  PY_PREFIX=`python -c 'import sys ; print sys.prefix'`
	  PY_VERSION=`python -c 'import sys ; print sys.version[[0:3]]'`
	  AC_MSG_CHECKING(for $PY_PREFIX/include/python$PY_VERSION/Python.h)
	  if test -f $PY_PREFIX/include/python$PY_VERSION/Python.h; then
		  AC_MSG_RESULT(yes)
		  PY_CFLAGS="-I$PY_PREFIX/include/python$PY_VERSION"
		  
		  if test "x$with_python_sitelib_dir" = "x" ; then
		    PY_MODULES_PATH="$PY_PREFIX/lib/python$PY_VERSION/site-packages"
		  else
		    PY_MODULES_PATH=$with_python_sitelib_dir
		  fi
		  
		  AC_SUBST([PY_VERSION])
		  AC_SUBST([PY_CFLAGS])
		  AC_SUBST([PY_MODULES_PATH])
		  if test "x$have_swig" = "xno"; then
			build_python=no
		  fi
	  else
		  AC_MSG_RESULT(no)
		  build_python=no
	  fi
fi

AM_CONDITIONAL(WITH_PYTHON, test "x$build_python" = "xyes")

dnl =======================================================================================

build_perl=no

AC_ARG_ENABLE(perl, [  --enable-perl		build perl binding [default=no]], build_perl="$enableval", build_perl=no)

AC_ARG_WITH(perl-sitelib-dir, [  --with-perl-sitelib-dir=PATH		path to perl site lib ])

AC_CHECK_PROG(have_perl, "perl", yes, no)

if test "x$have_perl" = "xyes"; then
	  PE_PREFIX=`(perl -e 'use Config; print $Config{archlib};')`
	  AC_MSG_CHECKING(for $PE_PREFIX/CORE/perl.h)
	  if test -f $PE_PREFIX/CORE/perl.h; then
		  AC_MSG_RESULT(yes)
		  PE_CFLAGS="-I$PE_PREFIX/CORE "
		  PE_CFLAGS=$PE_CFLAGS`(perl -e 'use Config; print $Config{cppflags};')`
		  
		  if test "x$with_perl_sitelib_dir" = "x" ; then
		    PE_MODULES_PATH=`(perl -e 'use Config; print $Config{sitelib};')`
		  else
		    PE_MODULES_PATH=$with_perl_sitelib_dir
		  fi
		  		  
		  AC_SUBST([PE_CFLAGS])
		  AC_SUBST([PE_MODULES_PATH])
		  if test "x$have_swig" = "xno"; then
			build_perl=no
		  fi
	  else
		  AC_MSG_RESULT(no)
		  build_perl=no
	  fi
fi

dnl FIXME: on fedora PE_MODULES_PATH="/usr/lib/perl5/site_perl"

AM_CONDITIONAL(WITH_PERL, test "x$build_perl" = "xyes")

dnl =======================================================================================

dnl =======================================================================================

AC_SUBST(PACKAGE_CFLAGS)
AC_SUBST(PACKAGE_LIBS)

dnl =======================================================================================

AC_OUTPUT([
Makefile
libhocr.pc
src/Makefile
po/Makefile.in
tests/Makefile
examples/Makefile
examples/hocr-gtk/Makefile
examples/hocr/Makefile
bindings/Makefile
bindings/python/Makefile
bindings/perl/Makefile
])

dnl ===========================================================================================
echo "
$PACKAGE-$VERSION

	prefix:				${prefix}
	compiler:			${CC}
	native language support		${nls}
	
	have swig			${have_swig}
	have python			${have_python}
	have perl			${have_perl}

	build hocr			${cmd}
	build hocr-gtk			${gtk}
		with spellchecker	${gtkspell}

	build python binding		${build_python}
	build perl binding		${build_perl}
	
	perl site lib path:		$PE_MODULES_PATH
	python site lib path:		$PY_MODULES_PATH

"
